group 'net.cyllene.hackerrank'
version '0.0.3'

apply plugin: 'java'
apply plugin: 'idea'

sourceCompatibility = 1.7

task jarComplete(type: Jar, dependsOn: jar) {
	description = 'Assembles a jar archive containing the main classes and dependencies.'
	group = 'build'

	from {
		configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
	} {
		// META-INF copies of dependencies would overwrite each other
		exclude 'META-INF/'
	}

	manifest {
		attributes 'Main-Class': 'net.cyllene.hackerrank.downloader.HackerrankDownloader',
				'Built-Date': new Date(),
				'Built-JDK': System.getProperty('java.version')
	}

	with jar
}

repositories {
	mavenCentral()
}

dependencies {
	testCompile 'junit:junit:4.11'
	testCompile 'org.hamcrest:hamcrest-core:1.3'
	testCompile 'org.mockito:mockito-core:1.10.19'
	compile 'org.apache.httpcomponents:httpclient:4.5.2'
	compile 'com.fasterxml.jackson.core:jackson-databind:2.7.3'
	compile 'commons-cli:commons-cli:1.3.1'
}

// Workaround for Windows
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
test { systemProperty "file.encoding", "utf-8" }